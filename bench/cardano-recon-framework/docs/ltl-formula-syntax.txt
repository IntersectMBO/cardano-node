s ::= "..."        where ... stands for a list of arbitrary unicode characters such that each is:
                      — not a control
                      — if whitespace, then simple ' '
                      — not a double quote
x ::= [_a-zA-Z][_a-zA-Z0-9]*
v ::= <int> | s               // property value, examples:
                              //   — 42
                              //   — "hello"
v̄ ::= {v, ..., v}             // set of property values, examples:
                              //   — {42, "hello"}
                              //   — {}
                              //   — {1, 2, 3, "bye"}
t ::= <int> | s | x           // property term, examples:
                              //   — 42
                              //   — "hello"
                              //   — x
                              //   — this_is_variable
c ::= s = t                   // property constraint, examples:
                              //   — "slot" = 1
                              //   — "host" = "macbook-pro"
                              //   — "slot" = x
c̄ ::= {c, ..., c}             // set of property constraints, examples:
                              //   — {"slot" = x}
                              //   — {"slot" = 42, "blockHash" = "xyz"}
                              //   — {}
ns ::= s                      // namespace, examples:
                              //   — "Forge.Loop.ForgedBlock"
                              //   — "Forge.Loop.NodeIsLeader"

φ{atom} ::= ⊤ | ⊥ | ns c̄ | (φ{≥universe})
φ{eq} ::= t = v
φ{prefix} ::= ◯ φ{≥atom} | ◯ᵏ φ{≥atom} | ♢ᵏ φ{≥atom} | ☐ ᪲ₖ φ{≥atom} | ☐ᵏ φ{≥atom} | ¬ φ{≥atom}
φ{and} ::= φ{>and} ∧ φ{≥and}
φ{or} ::= φ{>or} ∨ φ{≥or}
φ{implies} ::= φ{>implies} ⇒ φ{≥implies}
φ{universe} ::= ∀(x ∈ v̄). φ{≥universe}
              | ∀x. φ{≥universe} | φ{≥implies} \|ᵏ φ{≥implies}

atom < eq = prefix < and < or < implies < universe
