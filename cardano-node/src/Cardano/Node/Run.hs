{-# LANGUAGE BangPatterns        #-}
{-# LANGUAGE CPP                 #-}
{-# LANGUAGE DataKinds           #-}
{-# LANGUAGE FlexibleContexts    #-}
{-# LANGUAGE GADTs               #-}
{-# LANGUAGE NamedFieldPuns      #-}
{-# LANGUAGE PackageImports      #-}
{-# LANGUAGE ScopedTypeVariables #-}

#if !defined(mingw32_HOST_OS)
#define UNIX
#endif

module Cardano.Node.Run
  ( runNode
  , checkVRFFilePermissions
  ) where

import           Cardano.Prelude hiding (ByteString, atomically, take, trace)
import           Prelude (String)

import qualified Control.Concurrent.Async as Async
import           Control.Monad.Trans.Except.Extra (left)
import           "contra-tracer" Control.Tracer
import           Data.Text (breakOn, pack, take)
import qualified Data.Text as Text
import           Data.Time.Clock (UTCTime, getCurrentTime)
import           Data.Time.Clock.POSIX (utcTimeToPOSIXSeconds)
import           Data.Version (showVersion)
import           Network.HostName (getHostName)
import           Network.Socket (AddrInfo, Socket)
import           System.Directory (canonicalizePath, createDirectoryIfMissing,
                     makeAbsolute)
import           System.Environment (lookupEnv)
#ifdef UNIX
import           System.Posix.Files
import           System.Posix.Types (FileMode)
#else
import           System.Win32.File
#endif

import           Cardano.BM.Data.LogItem (LOContent (..), LogObject (..),
                     PrivacyAnnotation (..), mkLOMeta)
import           Cardano.BM.Data.Tracer (ToLogObject (..),
                     TracingVerbosity (..))
import           Cardano.BM.Data.Transformers (setHostname)
import           Cardano.BM.Trace
import           Paths_cardano_node (version)

import qualified Cardano.Crypto.Libsodium as Crypto

import qualified Cardano.Logging as NL
import           Cardano.Node.Configuration.Logging (EKGDirect (..),
                     LoggingLayer (..), Severity (..), createLoggingLayer,
                     nodeBasicInfo, shutdownLoggingLayer)
import           Cardano.Node.Configuration.POM (NodeConfiguration (..),
                     PartialNodeConfiguration (..),
                     defaultPartialNodeConfiguration, makeNodeConfiguration,
                     ncProtocol, parseNodeConfigurationFP)
import           Cardano.Node.Types
import           Cardano.TraceDispatcher.BasicInfo.Combinators (getBasicInfo)
import           Cardano.TraceDispatcher.BasicInfo.Types
import           Cardano.TraceDispatcher.Era.Byron ()
import           Cardano.TraceDispatcher.Era.Shelley ()
import           Cardano.TraceDispatcher.Tracers (mkDispatchTracers)
import           Cardano.Tracing.Config (TraceOptions (..), TraceSelection (..))
import           Cardano.Tracing.Constraints (TraceConstraints)

import qualified Ouroboros.Consensus.BlockchainTime.WallClock.Types as WCT
import           Ouroboros.Consensus.Cardano.Block
import           Ouroboros.Consensus.Cardano.CanHardFork
import qualified Ouroboros.Consensus.Config as Consensus
import           Ouroboros.Consensus.Config.SupportsNode
                     (ConfigSupportsNode (..))
import           Ouroboros.Consensus.HardFork.Combinator.Degenerate
import           Ouroboros.Consensus.Node (DiffusionArguments (..),
                     DiffusionTracers (..), DnsSubscriptionTarget (..),
                     IPSubscriptionTarget (..), RunNode, RunNodeArgs (..),
                     StdRunNodeArgs (..))
import qualified Ouroboros.Consensus.Node as Node (getChainDB, run)
import           Ouroboros.Consensus.Node.ProtocolInfo
import           Ouroboros.Consensus.Shelley.Ledger.Ledger
import           Ouroboros.Consensus.Util.Orphans ()
import           Ouroboros.Network.IOManager (withIOManager)
import           Ouroboros.Network.NodeToNode (AcceptedConnectionsLimit (..),
                     DiffusionMode)
import qualified Shelley.Spec.Ledger.API as SL

import           Cardano.Api
import qualified Cardano.Api.Protocol.Types as Protocol

import           Cardano.Config.Git.Rev (gitRev)

import           Trace.Forward.Protocol.Type (NodeInfo (..))

import           Cardano.Node.Configuration.Socket (SocketOrSocketInfo (..),
                     gatherConfiguredSockets, getSocketOrSocketInfoAddr,
                     renderSocketConfigError)
import           Cardano.Node.Configuration.Topology
import           Cardano.Node.Handlers.Shutdown
import           Cardano.Node.Protocol (mkConsensusProtocol)
import           Cardano.Node.Protocol.Types
import           Cardano.Tracing.Kernel
import           Cardano.Tracing.Peer
import           Cardano.Tracing.Tracers

{- HLINT ignore "Use fewer imports" -}

runNode
  :: PartialNodeConfiguration
  -> IO ()
runNode cmdPc = do
    now <- getCurrentTime
    -- TODO: Remove sodiumInit: https://github.com/input-output-hk/cardano-base/issues/175
    Crypto.sodiumInit

    configYamlPc <- parseNodeConfigurationFP . getLast $ pncConfigFile cmdPc

    nc <- case makeNodeConfiguration $ defaultPartialNodeConfiguration <> configYamlPc <> cmdPc of
            Left err -> panic $ "Error in creating the NodeConfiguration: " <> Text.pack err
            Right nc' -> return nc'

    case shelleyVRFFile $ ncProtocolFiles nc of
      Just vrfFp -> do vrf <- runExceptT $ checkVRFFilePermissions vrfFp
                       case vrf of
                         Left err ->
                           putTextLn (renderVRFPrivateKeyFilePermissionError err) >> exitFailure
                         Right () ->
                           pure ()
      Nothing -> pure ()

    eitherSomeProtocol <- runExceptT $ mkConsensusProtocol nc

    p :: SomeConsensusProtocol <-
      case eitherSomeProtocol of
        Left err -> putStrLn (displayError err) >> exitFailure
        Right p  -> pure p

    eLoggingLayer <- runExceptT $ createLoggingLayer
                     (ncTraceConfig nc)
                     (Text.pack (showVersion version))
                     nc
                     p

    loggingLayer <- case eLoggingLayer of
                      Left err  -> putTextLn (show err) >> exitFailure
                      Right res -> return res

    -- New logging initialisation
    loggerConfiguration <-
      case getLast $ pncConfigFile cmdPc of
        Just fileName -> NL.readConfiguration (unConfigPath fileName)
        Nothing -> putTextLn "No configuration file name found!" >> exitFailure
    baseTrace    <- NL.standardTracer Nothing
    nodeInfo <- prepareNodeInfo nc p loggerConfiguration now
    forwardTrace <- withIOManager $ \iomgr -> NL.forwardTracer iomgr loggerConfiguration nodeInfo
    mbEkgTrace   <- case llEKGDirect loggingLayer of
                      Nothing -> pure Nothing
                      Just ekgDirect ->
                        liftM Just (NL.ekgTracer (Right (ekgServer ekgDirect)))
    -- End new logging initialisation

    !trace <- setupTrace loggingLayer
    let tracer = contramap pack $ toLogObject trace

    logTracingVerbosity nc tracer

    let handleNodeWithTracers
          :: ( TraceConstraints blk
             , Protocol.Protocol IO blk
             )
          => Protocol.ProtocolInfoArgs IO blk
          -> IO ()
        handleNodeWithTracers runP = do
          -- This IORef contains node kernel structure which holds node kernel.
          -- Used for ledger queries and peer connection status.
          nodeKernelData <- mkNodeKernelData
          let ProtocolInfo { pInfoConfig = cfg } = Protocol.protocolInfo runP
          let fp = case getLast (pncConfigFile cmdPc) of
                      Just fileName -> unConfigPath fileName
                      Nothing       -> "No file path found!"
          bi <- getBasicInfo nc p fp
          tracers <- mkDispatchTracers
                       (Consensus.configBlock cfg)
                       (ncTraceConfig nc)
                       trace
                       nodeKernelData
                       (llEKGDirect loggingLayer)
                       baseTrace
                       forwardTrace
                       mbEkgTrace
                       loggerConfiguration
                       bi

          Async.withAsync (handlePeersListSimple trace nodeKernelData)
              $ \_peerLogingThread ->
                -- We ignore peer loging thread if it dies, but it will be killed
                -- when 'handleSimpleNode' terminates.
                handleSimpleNode p runP trace tracers nc (setNodeKernel nodeKernelData)
                `finally`
                shutdownLoggingLayer loggingLayer

    case p of
      SomeConsensusProtocol _ runP -> handleNodeWithTracers runP

logTracingVerbosity :: NodeConfiguration -> Tracer IO String -> IO ()
logTracingVerbosity nc tracer =
  case ncTraceConfig nc of
    TracingOff -> return ()
    TracingOn traceConf ->
      case traceVerbosity traceConf of
        NormalVerbosity -> traceWith tracer "tracing verbosity = normal verbosity "
        MinimalVerbosity -> traceWith tracer "tracing verbosity = minimal verbosity "
        MaximalVerbosity -> traceWith tracer "tracing verbosity = maximal verbosity "
    TraceDispatcher _traceConf ->
      pure ()
-- | Add the application name and unqualified hostname to the logging
-- layer basic trace.
--
-- If the @CARDANO_NODE_LOGGING_HOSTNAME@ environment variable is set,
-- it overrides the system hostname. This is useful when running a
-- local test cluster with all nodes on the same host.
setupTrace
  :: LoggingLayer
  -> IO (Trace IO Text)
setupTrace loggingLayer = do
    hn <- maybe hostname (pure . pack) =<< lookupEnv "CARDANO_NODE_LOGGING_HOSTNAME"
    return $
        setHostname hn $
        llAppendName loggingLayer "node" (llBasicTrace loggingLayer)
  where
    hostname = do
      hn0 <- pack <$> getHostName
      return $ take 8 $ fst $ breakOn "." hn0

handlePeersListSimple
  :: Trace IO Text
  -> NodeKernelData blk
  -> IO ()
handlePeersListSimple tr nodeKern = forever $ do
  getCurrentPeers nodeKern >>= tracePeers tr
  threadDelay 2000000 -- 2 seconds.

isOldLogging :: TraceOptions -> Bool
isOldLogging TracingOff          = False
isOldLogging (TracingOn _)       = True
isOldLogging (TraceDispatcher _) = False

isNewLogging :: TraceOptions -> Bool
isNewLogging TracingOff          = False
isNewLogging (TracingOn _)       = False
isNewLogging (TraceDispatcher _) = True

-- | Sets up a simple node, which will run the chain sync protocol and block
-- fetch protocol, and, if core, will also look at the mempool when trying to
-- create a new block.

handleSimpleNode
  :: forall blk
  . ( RunNode blk
    , Protocol.Protocol IO blk
    )
  => SomeConsensusProtocol
  -> Protocol.ProtocolInfoArgs IO blk
  -> Trace IO Text
  -> Tracers RemoteConnectionId LocalConnectionId blk
  -> NodeConfiguration
  -> (NodeKernel IO RemoteConnectionId LocalConnectionId blk -> IO ())
  -- ^ Called on the 'NodeKernel' after creating it, but before the network
  -- layer is initialised.  This implies this function must not block,
  -- otherwise the node won't actually start.
  -> IO ()
handleSimpleNode scp runP trace nodeTracers nc onKernel = do
  meta <- mkLOMeta Notice Public

  let pInfo = Protocol.protocolInfo runP
      tracer = toLogObject trace

  if isOldLogging (ncTraceConfig nc)
    then createTracers nc trace tracer
    else pure ()

  (publicIPv4SocketOrAddr, publicIPv6SocketOrAddr, localSocketOrPath) <- do
    result <- runExceptT (gatherConfiguredSockets nc)
    case result of
      Right triplet -> return triplet
      Left error -> do
        traceNamedObject
          (appendName "error" trace)
          (meta, LogMessage (Text.pack (renderSocketConfigError error)))
        throwIO error

  dbPath <- canonDbPath nc

  eitherTopology <- readTopologyFile nc
  nt <- either (\err -> panic $ "Cardano.Node.Run.handleSimpleNode.readTopologyFile: " <> err) pure eitherTopology

  let diffusionTracers :: DiffusionTracers
      diffusionTracers = createDiffusionTracers nodeTracers

      (ipProducerAddrs, dnsProducerAddrs) = producerAddresses nt

      dnsProducers :: [DnsSubscriptionTarget]
      dnsProducers = uncurry dnsSubscriptionTarget `map` dnsProducerAddrs

      ipProducers :: IPSubscriptionTarget
      ipProducers = ipSubscriptionTargets ipProducerAddrs

      diffusionArguments :: DiffusionArguments
      diffusionArguments =
        createDiffusionArguments
          publicIPv4SocketOrAddr
          publicIPv6SocketOrAddr
          localSocketOrPath
          (ncDiffusionMode nc)
          ipProducers
          dnsProducers

  ipv4 <- traverse getSocketOrSocketInfoAddr publicIPv4SocketOrAddr
  ipv6 <- traverse getSocketOrSocketInfoAddr publicIPv6SocketOrAddr

  if isOldLogging (ncTraceConfig nc)
    then do
      traceNamedObject
        (appendName "addresses" trace)
        (meta, LogMessage . Text.pack . show $ catMaybes [ipv4, ipv6])
      traceNamedObject
        (appendName "diffusion-mode" trace)
        (meta, LogMessage . Text.pack . show . ncDiffusionMode $ nc)
      traceNamedObject
        (appendName "dns-producers" trace)
        (meta, LogMessage . Text.pack . show $ dnsProducers)
      traceNamedObject
        (appendName "ip-producers" trace)
        (meta, LogMessage . Text.pack . show $ ipProducers)
    else if isNewLogging (ncTraceConfig nc)
      then do
        let bin = BasicInfoNetwork {
                    niAddresses     = catMaybes [ipv4, ipv6]
                  , niDiffusionMode = ncDiffusionMode $ nc
                  , niDnsProducers  = dnsProducers
                  , niIpProducers   = ipProducers
                  }
        traceWith (basicInfoTracer nodeTracers) (BINetwork bin)
      else pure ()

  withShutdownHandling nc trace $ \sfds ->
   Node.run
     RunNodeArgs
       { rnTraceConsensus = consensusTracers nodeTracers
       , rnTraceNTN       = nodeToNodeTracers nodeTracers
       , rnTraceNTC       = nodeToClientTracers nodeTracers
       , rnProtocolInfo   = pInfo
       , rnNodeKernelHook = \registry nodeKernel -> do
           maybeSpawnOnSlotSyncedShutdownHandler nc sfds trace registry
             (Node.getChainDB nodeKernel)
           onKernel nodeKernel
       }
     StdRunNodeArgs
       { srnBfcMaxConcurrencyBulkSync   = unMaxConcurrencyBulkSync <$> ncMaxConcurrencyBulkSync nc
       , srnBfcMaxConcurrencyDeadline   = unMaxConcurrencyDeadline <$> ncMaxConcurrencyDeadline nc
       , srnChainDbValidateOverride     = ncValidateDB nc
       , srnSnapshotInterval            = ncSnapshotInterval nc
       , srnDatabasePath                = dbPath
       , srnDiffusionArguments          = diffusionArguments
       , srnDiffusionTracers            = diffusionTracers
       , srnEnableInDevelopmentVersions = ncTestEnableDevelopmentNetworkProtocols nc
       , srnTraceChainDB                = chainDBTracer nodeTracers
       }
 where
  createDiffusionTracers :: Tracers RemoteConnectionId LocalConnectionId blk
                         -> DiffusionTracers
  createDiffusionTracers nodeTracers' = DiffusionTracers
    { dtIpSubscriptionTracer = ipSubscriptionTracer nodeTracers'
    , dtDnsSubscriptionTracer = dnsSubscriptionTracer nodeTracers'
    , dtDnsResolverTracer = dnsResolverTracer nodeTracers'
    , dtErrorPolicyTracer = errorPolicyTracer nodeTracers'
    , dtLocalErrorPolicyTracer = localErrorPolicyTracer nodeTracers'
    , dtAcceptPolicyTracer = acceptPolicyTracer nodeTracers'
    , dtMuxTracer = muxTracer nodeTracers'
    , dtMuxLocalTracer = muxLocalTracer nodeTracers'
    , dtHandshakeTracer = handshakeTracer nodeTracers'
    , dtHandshakeLocalTracer = localHandshakeTracer nodeTracers'
    , dtDiffusionInitializationTracer = diffusionInitializationTracer nodeTracers'
    , dtLedgerPeersTracer = nullTracer -- TODO network team
    }

  createTracers
    :: NodeConfiguration
    -> Trace IO Text
    -> Tracer IO Text
    -> IO ()
  createTracers NodeConfiguration { ncValidateDB }
                tr tracer = do
    let ProtocolInfo{ pInfoConfig = cfg } = Protocol.protocolInfo runP

    meta <- mkLOMeta Notice Public
    traceNamedObject (appendName "networkMagic" tr)
                     (meta, LogMessage ("NetworkMagic " <> show (unNetworkMagic . getNetworkMagic $ Consensus.configBlock cfg)))

    startTime <- getCurrentTime
    traceNodeBasicInfo tr =<< nodeBasicInfo nc scp startTime
    traceCounter "nodeStartTime" tr (ceiling $ utcTimeToPOSIXSeconds startTime)

    when ncValidateDB $ traceWith tracer "Performing DB validation"

  traceNodeBasicInfo :: Trace IO Text -> [LogObject Text] -> IO ()
  traceNodeBasicInfo tr basicInfoItems =
    forM_ basicInfoItems $ \(LogObject nm mt content) ->
      traceNamedObject (appendName nm tr) (mt, content)

--------------------------------------------------------------------------------
-- Helper functions
--------------------------------------------------------------------------------

canonDbPath :: NodeConfiguration -> IO FilePath
canonDbPath NodeConfiguration{ncDatabaseFile = DbFile dbFp} = do
  fp <- canonicalizePath =<< makeAbsolute dbFp
  createDirectoryIfMissing True fp
  return fp


-- | Make sure the VRF private key file is readable only
-- by the current process owner the node is running under.
checkVRFFilePermissions :: FilePath -> ExceptT VRFPrivateKeyFilePermissionError IO ()
#ifdef UNIX
checkVRFFilePermissions vrfPrivKey = do
  fs <- liftIO $ getFileStatus vrfPrivKey
  let fm = fileMode fs
  -- Check the the VRF private key file does not give read/write/exec permissions to others.
  when (hasOtherPermissions fm)
       (left $ OtherPermissionsExist vrfPrivKey)
  -- Check the the VRF private key file does not give read/write/exec permissions to any group.
  when (hasGroupPermissions fm)
       (left $ GroupPermissionsExist vrfPrivKey)
 where
  hasPermission :: FileMode -> FileMode -> Bool
  hasPermission fModeA fModeB = fModeA `intersectFileModes` fModeB /= nullFileMode

  hasOtherPermissions :: FileMode -> Bool
  hasOtherPermissions fm' = fm' `hasPermission` otherModes

  hasGroupPermissions :: FileMode -> Bool
  hasGroupPermissions fm' = fm' `hasPermission` groupModes
#else
checkVRFFilePermissions vrfPrivKey = do
  attribs <- liftIO $ getFileAttributes vrfPrivKey
  -- https://docs.microsoft.com/en-us/windows/win32/api/fileapi/nf-fileapi-createfilea
  -- https://docs.microsoft.com/en-us/windows/win32/fileio/file-access-rights-constants
  -- https://docs.microsoft.com/en-us/windows/win32/secauthz/standard-access-rights
  -- https://docs.microsoft.com/en-us/windows/win32/secauthz/generic-access-rights
  -- https://docs.microsoft.com/en-us/windows/win32/secauthz/access-mask
  when (attribs `hasPermission` genericPermissions)
       (left $ GenericPermissionsExist vrfPrivKey)
 where
  genericPermissions = gENERIC_ALL .|. gENERIC_READ .|. gENERIC_WRITE .|. gENERIC_EXECUTE
  hasPermission fModeA fModeB = fModeA .&. fModeB /= gENERIC_NONE
#endif

createDiffusionArguments
  :: Maybe (SocketOrSocketInfo Socket AddrInfo)
   -- ^ Either a socket bound to IPv4 address provided by systemd or IPv4
   -- address to bind to for NodeToNode communication.
  -> Maybe (SocketOrSocketInfo Socket AddrInfo)
   -- ^ Either a socket bound to IPv6 address provided by systemd or IPv6
   -- address to bind to for NodeToNode communication.
  -> Maybe (SocketOrSocketInfo Socket SocketPath)
  -- ^ Either a SOCKET_UNIX socket provided by systemd or a path for
  -- NodeToClient communication.
  -> DiffusionMode
  -> IPSubscriptionTarget
  -> [DnsSubscriptionTarget]
  -> DiffusionArguments
createDiffusionArguments publicIPv4SocketsOrAddrs
                         publicIPv6SocketsOrAddrs
                         mLocalSocketOrPath
                         diffusionMode
                         ipProducers dnsProducers
                         =
  DiffusionArguments
    -- This is not elegant, but it will change once `coot/connection-manager` is
    -- merged into `ouroboros-networ`.
    { daIPv4Address = eitherSocketOrSocketInfo <$> publicIPv4SocketsOrAddrs
    , daIPv6Address = eitherSocketOrSocketInfo <$> publicIPv6SocketsOrAddrs
    , daLocalAddress = mLocalSocketOrPath >>= return . fmap unSocketPath
                                                     . eitherSocketOrSocketInfo
    , daIpProducers  = ipProducers
    , daDnsProducers = dnsProducers
    -- TODO: these limits are arbitrary at the moment;
    -- issue: https://github.com/input-output-hk/ouroboros-network/issues/1836
    , daAcceptedConnectionsLimit = AcceptedConnectionsLimit {
        acceptedConnectionsHardLimit = 512
      , acceptedConnectionsSoftLimit = 384
      , acceptedConnectionsDelay     = 5
      }
    , daDiffusionMode = diffusionMode
    }
  where
    eitherSocketOrSocketInfo :: SocketOrSocketInfo a b -> Either a b
    eitherSocketOrSocketInfo (ActualSocket a) = Left a
    eitherSocketOrSocketInfo (SocketInfo b)   = Right b

dnsSubscriptionTarget :: NodeDnsAddress -> Int -> DnsSubscriptionTarget
dnsSubscriptionTarget na valency =
  DnsSubscriptionTarget { dstDomain  = nodeHostDnsAddressToDomain (naHostAddress na)
                        , dstPort    = naPort na
                        , dstValency = valency
                        }

ipSubscriptionTargets :: [NodeIPAddress] -> IPSubscriptionTarget
ipSubscriptionTargets ipProdAddrs =
  let ips = nodeAddressToSockAddr <$> ipProdAddrs
  in IPSubscriptionTarget { ispIps = ips
                          , ispValency = length ips
                          }


producerAddresses
  :: NetworkTopology
  -> ( [NodeIPAddress]
     , [(NodeDnsAddress, Int)])
producerAddresses nt =
  case nt of
    RealNodeTopology producers' ->
        partitionEithers
      . mapMaybe remoteAddressToNodeAddress
      $ producers'
    MockNodeTopology nodeSetup ->
        partitionEithers
      . mapMaybe remoteAddressToNodeAddress
      . concatMap producers
      $ nodeSetup

-- | Prepare basic info about the node. This info will be sent to 'cardano-tracer'.
prepareNodeInfo
  :: NodeConfiguration
  -> SomeConsensusProtocol
  -> NL.TraceConfig
  -> UTCTime
  -> IO NodeInfo
prepareNodeInfo nc (SomeConsensusProtocol whichP pForInfo) tc nodeStartTime = do
  nodeName <- prepareNodeName
  return $ NodeInfo
    { niName            = nodeName
    , niProtocol        = pack . protocolName $ ncProtocol nc
    , niVersion         = pack . showVersion $ version
    , niCommit          = gitRev
    , niStartTime       = nodeStartTime
    , niSystemStartTime = systemStartTime
    }
 where
  cfg = pInfoConfig $ Protocol.protocolInfo pForInfo

  systemStartTime :: UTCTime
  systemStartTime =
    case whichP of
      Protocol.ByronBlockType ->
        getSystemStartByron
      Protocol.ShelleyBlockType ->
        let DegenLedgerConfig cfgShelley = Consensus.configLedger cfg
        in getSystemStartShelley cfgShelley
      Protocol.CardanoBlockType ->
        let CardanoLedgerConfig _ cfgShelley cfgAllegra cfgMary cfgAlonzo = Consensus.configLedger cfg
        in minimum [ getSystemStartByron
                   , getSystemStartShelley cfgShelley
                   , getSystemStartShelley cfgAllegra
                   , getSystemStartShelley cfgMary
                   , getSystemStartShelley cfgAlonzo
                   ]

  getSystemStartByron = WCT.getSystemStart . getSystemStart . Consensus.configBlock $ cfg
  getSystemStartShelley cfg' = SL.sgSystemStart . shelleyLedgerGenesis . shelleyLedgerConfig $ cfg'

  prepareNodeName =
    case NL.tcNodeName tc of
      Just aName -> return aName
      Nothing -> pack <$> getHostName
